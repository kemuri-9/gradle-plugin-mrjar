/**
 * Copyright 2021 Steven Walters
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * Test the following behaviors:
 * * main java 8 code
 * * with java 9 specific code
 * * with java 10 specific code
 * * with groovy tests for each
 * * with modularity
 * * utilizing matching JVMs toolchains for each
 */

plugins {
    id 'java-library'
    id 'groovy'
    id 'net.kemuri9.gradle.mrjar'
}

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain.languageVersion = JavaLanguageVersion.of(8)
}

sourceSets {
    main {
        java {
            srcDir '../../src/test/resources/code/src/main/java'
        }
    }
    test {
        groovy {
            srcDir '../../src/test/resources/code/src/test/groovy'
        }
    }
}

mrjar {
    useToolchain = true
    includeBaseTests = true
    useJarInTests = true
    baseVersion {
        main {
            patchesModule = 'test'
        }
    }
    addVersion(9) {
        main {
            java {
                documentation {
                    options.showFromProtected().addBooleanOption('html5', true)
                }
                src {
                    srcDir('../../src/test/resources/code/src/java9/java')
                }
            }
            dependsOn(8) {
                extendConfigurations = true
            }
            registerSourceJar()
        }
        test {
            groovy {
                src {
                    srcDir('../../src/test/resources/code/src/java9Test/groovy')
                }
            }
            dependsOn(8) {
                extendConfigurations = true
            }
        }
        primaryModuleDefinition = true
    }
    addVersion(10) {
        main {
            patchesModule = 'test'
            java {
                documentation {
                    options.showFromProtected().addBooleanOption('html5', true)
                }
                src {
                    srcDir('../../src/test/resources/code/src/java10/java')
                }
            }
            dependsOn(8) {
                extendConfigurations = true
            }
            dependsOn 9
            registerSourceJar()
        }
        test {
            dependsOn(8) {
                extendConfigurations = true
            }
            groovy {
                src {
                    srcDir('../../src/test/resources/code/src/java10Test/groovy')
                }
            }
        }
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
    defaultCharacterEncoding = 'UTF-8'

    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) {
                System.out.print "${getPath()} results: ${result.resultType} (${result.testCount} tests, "
                System.out.print "${result.successfulTestCount} passed, ${result.failedTestCount} failed, "
                System.out.println "${result.skippedTestCount} skipped)"
            }
        }
    }
}

task listArtifacts {
    doLast {
        println configurations.collect{ conf -> conf.artifacts.collect{ it.file.name } }.flatten().unique()
    }
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    testImplementation localGroovy()
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
}
